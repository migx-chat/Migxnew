apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

def projectRoot = rootDir.getAbsoluteFile().getParentFile().getAbsolutePath()

react {
    entryFile = file(["node", "-e", "require('expo/scripts/resolveAppEntry')", projectRoot, "android", "absolute"]
            .execute(null, rootDir).text.trim())

    reactNativeDir = new File(
            ["node", "--print", "require.resolve('react-native/package.json')"]
                    .execute(null, rootDir).text.trim()
    ).getParentFile().getAbsoluteFile()

    hermesCommand = new File(
            ["node", "--print", "require.resolve('react-native/package.json')"]
                    .execute(null, rootDir).text.trim()
    ).getParentFile().getAbsolutePath() + "/sdks/hermesc/%OS-BIN%/hermesc"

    codegenDir = new File(
            ["node", "--print", "require.resolve('@react-native/codegen/package.json', { paths: [require.resolve('react-native/package.json')] })"]
                    .execute(null, rootDir).text.trim()
    ).getParentFile().getAbsoluteFile()

    cliFile = new File(
            ["node", "--print", "require.resolve('@expo/cli', { paths: [require.resolve('expo/package.json')] })"]
                    .execute(null, rootDir).text.trim()
    )

    bundleCommand = "export:embed"
    autolinkLibrariesWithApp()
}

def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {

    namespace "com.migx.app"
    compileSdk 35
    ndkVersion "26.1.10909125"

    externalNativeBuild {
        cmake {
            version "3.22.1"
        }
    }

    defaultConfig {
        applicationId "com.migx.app"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "1.0.0"

        buildConfigField "String", "REACT_NATIVE_RELEASE_LEVEL", "\"stable\""

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++20 -D_LIBCPP_ENABLE_CXX20_REMOVED_FEATURES"
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    "proguard-android-optimize.txt"
            ), "proguard-rules.pro"
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
        pickFirst "**/libc++_shared.so"
        pickFirst "**/libfbjni.so"
    }

    androidResources {
        ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:!CVS:!thumbs.db:!picasa.ini:!*~'
    }
}

dependencies {

    implementation("com.facebook.react:react-android")

    def isGifEnabled = (findProperty('expo.gif.enabled') ?: "") == "true"
    def isWebpEnabled = (findProperty('expo.webp.enabled') ?: "") == "true"
    def isWebpAnimatedEnabled = (findProperty('expo.webp.animated') ?: "") == "true"

    if (isGifEnabled) {
        implementation("com.facebook.fresco:animated-gif:${expoLibs.versions.fresco.get()}")
    }

    if (isWebpEnabled) {
        implementation("com.facebook.fresco:webpsupport:${expoLibs.versions.fresco.get()}")
        if (isWebpAnimatedEnabled) {
            implementation("com.facebook.fresco:animated-webp:${expoLibs.versions.fresco.get()}")
        }
    }

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
